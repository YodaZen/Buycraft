
# ██████╗░██╗░░░██╗██╗░░░██╗░█████╗░██████╗░░█████╗░███████╗████████╗  ██╗░░░░░░█████╗░██╗░░░██╗███████╗████████╗
# ██╔══██╗██║░░░██║╚██╗░██╔╝██╔══██╗██╔══██╗██╔══██╗██╔════╝╚══██╔══╝  ██║░░░░░██╔══██╗██║░░░██║██╔════╝╚══██╔══╝
# ██████╦╝██║░░░██║░╚████╔╝░██║░░╚═╝██████╔╝███████║█████╗░░░░░██║░░░  ██║░░░░░███████║╚██╗░██╔╝█████╗░░░░░██║░░░
# ██╔══██╗██║░░░██║░░╚██╔╝░░██║░░██╗██╔══██╗██╔══██║██╔══╝░░░░░██║░░░  ██║░░░░░██╔══██║░╚████╔╝░██╔══╝░░░░░██║░░░
# ██████╦╝╚██████╔╝░░░██║░░░╚█████╔╝██║░░██║██║░░██║██║░░░░░░░░██║░░░  ███████╗██║░░██║░░╚██╔╝░░███████╗░░░██║░░░
# ╚═════╝░░╚═════╝░░░░╚═╝░░░░╚════╝░╚═╝░░╚═╝╚═╝░░╚═╝╚═╝░░░░░░░░╚═╝░░░  ╚══════╝╚═╝░░╚═╝░░░╚═╝░░░╚══════╝░░░╚═╝░░░

# ░█████╗░███████╗  ██╗░░░██╗░█████╗░██████╗░░█████╗░███████╗███████╗███╗░░██╗
# ██╔══██╗██╔════╝  ╚██╗░██╔╝██╔══██╗██╔══██╗██╔══██╗╚════██║██╔════╝████╗░██║
# ███████║█████╗░░  ░╚████╔╝░██║░░██║██║░░██║███████║░░███╔═╝█████╗░░██╔██╗██║
# ██╔══██║██╔══╝░░  ░░╚██╔╝░░██║░░██║██║░░██║██╔══██║██╔══╝░░██╔══╝░░██║╚████║
# ██║░░██║██║░░░░░  ░░░██║░░░╚█████╔╝██████╔╝██║░░██║███████╗███████╗██║░╚███║
# ╚═╝░░╚═╝╚═╝░░░░░  ░░░╚═╝░░░░╚════╝░╚═════╝░╚═╝░░╚═╝╚══════╝╚══════╝╚═╝░░╚══╝

command /buycraft [<string>] [<offline player>] [<integer>]:
    aliases: /butik, /buy, /store
    cooldown: 1 second
    cooldown message: %{buycraft::prefix::warning}% &fVent lidt før du prøver igen!
    trigger:
        set {_currency::2} to (yml value "Messages.currency.plural" from "plugins/Skript/YAML/Buycraft/Messages.yml")
        if arg-1 is not set:
            buycraft(player)
        else if arg-1 = "request":
            set {_num} to "%arg-2%" parsed as number
            if {_num} is set:
                set {_method} to (yml value "Payment.Method" from "plugins/Skript/YAML/Buycraft/Config.yml")
                if {_method} = "DIRECT TRANSFER":
                    PaymentRequest({_num},player)
                else if {_method} = "UNIKPAY":
                    PaymentRequest({_num},player)
            else:
                message "&8&l[&a&l!&8&l] &fForkert brug: /buycraft request <antal>!"
        if player has permission "buycraft.manage":
            if arg-1 = "info":
                if arg-2 is not set:
                    loop placeholder_replace((yml list "Messages.Buycraft_Info.CommandMessage" from "plugins/Skript/YAML/Buycraft/Messages.yml"),player):
                        message formatted loop-value to player
                else if arg-2 is a player:
                    loop placeholder_replace((yml list "Messages.Buycraft_Info.PlayerStats" from "plugins/Skript/YAML/Buycraft/Messages.yml"),player):
                        message formatted loop-value to player
            if arg-2 is a player:
                if arg-1 = "give":
                    if arg-3 is set:
                        add arg-3 to {Buycraft::%arg-2's uuid%::Coins}
                        message "&8&l[&a&l!&8&l] &b%arg-2%&f har købt &b%arg-3% %{_currency::2}%&f via buycraft!" to all players
                        add "&f- [%{_currency::2} in strict proper case%] %arg-3% &8(&f%now formatted as "dd-MM-yyyy, HH:mm"%&8)" to {Buycraft::%arg-2's uuid%::Purchases::*}
                else if arg-1 = "take":
                    if arg-3 is set:
                        if {Buycraft::%arg-2's uuid%::Coins} >= arg-3:
                            remove arg-3 from {Buycraft::%arg-2's uuid%::Coins}
                            message "&8&l[&a&l!&8&l] &fDu har fjernet &b%arg-3% %{_currency::2}%&f fra &b%arg-2%&f!" to player
                        else:
                            if {Buycraft::%arg-2's uuid%::Coins} > 0:
                                set {Buycraft::%arg-2's uuid%::Coins} to 0
                                message "&8&l[&a&l!&8&l] &fDu har sat &b%arg-2%'s %{_currency::2}%&f til 0!" to player
                            else:
                                message "&8&l[&c&l!&8&l] &b%arg-2%&f har allerede kun 0 %{_currency::2}%!" to player
                else if arg-1 = "clear":
                    delete {Buycraft::%arg-2's uuid%::*}
                    message "&8&l[&a&l!&8&l] &fDu har cleared &b%arg-2%" to player



inventory click:
    player's current inventory contains (metadata "Buycraft" of player)
    cancel event
    set {_type} to (metadata "Buycraft_Type" of player)
    loop yml node keys "Menues.%{_type}%.Items" from "plugins/Skript/YAML/Buycraft/Config.yml":
        set {_Slots::*} to (yml list "%loop-node%.Slots" from "plugins/Skript/YAML/Buycraft/Config.yml")
        if {_Slots::*} contains index of event-slot:
            set {_actions::*} to (yml list "%loop-node%.Settings.ClickAction" from "plugins/Skript/YAML/Buycraft/Config.yml")
            loop {_actions::*}:
                if loop-value-2 contains "[PLAYSOUND]":
                    set {_sound} to 2nd element of (split loop-value-2 at " ")
                    play sound {_sound} with volume 0.5 to player
                if loop-value-2 contains "[OPEN]":
                    set {_menues::*} to yml node keys "Menues" from "plugins/Skript/YAML/Buycraft/Config.yml"
                    set {_menu} to 2nd element of (split loop-value-2 at " ")
                    if {_menues::*} contains {_menu}:
                        buycraft(player,{_menu})
                    else:
                        message "&cKonfig Fejl: Kan ikke åbne menuen, da den ikke findes!" to player
                if loop-value-2 contains "[PAYMENT]":
                    set {_name} to (2nd element of (split loop-value-2 at " "))
                    Buycraft_Payment({_type},{_name},index of event-slot,player)
                if loop-value-2 contains "[CLOSE]":
                    close player's inventory


function Buycraft_Payment(type: string, name: string, slot: integer, player: player):
    set {_currency::2} to (yml value "Messages.currency.plural" from "plugins/Skript/YAML/Buycraft/Messages.yml")
    loop yml node keys "Menues.%{_type}%.Items" from "plugins/Skript/YAML/Buycraft/Config.yml":
        set {_slots::*} to (yml list "%loop-node%.Slots" from "plugins/Skript/YAML/Buycraft/Config.yml")
        if {_slots::*} contains {_slot}:
            set {_price} to (yml value "%loop-node%.Settings.Price" from "plugins/Skript/YAML/Buycraft/Config.yml")
            set {_buyable} to (yml value "%loop-node%.Settings.BuyAmounts" from "plugins/Skript/YAML/Buycraft/Config.yml")
            if {_buyable} is set:
                set {_amount} to (size of ({Buycraft::%{_player}'s uuid%::Purchases::*} where [input contains "[%{_type}%] %{_name}%"]))
                if {_amount} >= {_buyable}:
                    message "&8&l[&c&l!&8&l] &fDu kan ikke købe flere af dette produkt!" to {_player}
                    stop
            if ((yml value "%loop-node%.Settings.Priority" from "plugins/Skript/YAML/Buycraft/Config.yml") ? 1) > ({Buycraft::%{_player}'s uuid%::%{_name}%} ? 0):
                if {Buycraft::%{_player}'s uuid%::Coins} >= {_price}:
                    subtract {_price} from {Buycraft::%{_player}'s uuid%::Coins}
                    message "&8&l[&a&l!&8&l] &b%{_player}%&f har lige købt &b%{_name}%&f for &b%{_price}% %{_currency::2}%&f!" to all players
                    if (yml value "%loop-node%.Settings.Priority" from "plugins/Skript/YAML/Buycraft/Config.yml") is set:
                        set {Buycraft::%{_player}'s uuid%::%{_type}%} to (yml value "%loop-node%.Settings.Priority" from "plugins/Skript/YAML/Buycraft/Config.yml")
                    set {_command::*} to (yml list "%loop-node%.Settings.ExecuteCommand" from "plugins/Skript/YAML/Buycraft/Config.yml")
                    loop {_command::*}:
                        if loop-value-2 contains "[PLAYER]":
                            {_player} command "%placeholder_replace(loop-value-2,{_player})%"
                        else if loop-value-2 contains "[CONSOLE]":
                            console command "%placeholder_replace(loop-value-2,{_player})%"
                    add "&f- [%{_type}%] %{_name}% &8(&f%now formatted as "dd-MM-yyyy, HH:mm"%&8)" to {Buycraft::%{_player}'s uuid%::Purchases::*}
                else:
                    message "&8&l[&c&l!&8&l] &fDu har ikke nok %{_currency::2}% til at købe dette! Du har &b&n%{Buycraft::%{_player}'s uuid%::Coins} ? 0% %{_currency::2}%&f!" to {_player}
            else if ((yml value "%loop-node%.Settings.Priority" from "plugins/Skript/YAML/Buycraft/Config.yml") ? 1) = ({Buycraft::%{_player}'s uuid%::%{_name}%} ? 0):
                message "&8&l[&c&l!&8&l] &fDu ejer allerede dette produkt&f!" to {_player}
            else:
                message "&8&l[&c&l!&8&l] &fDu kan ikke dette da du allerede ejer et bedre produkt&f!" to {_player}
