
# ██████╗░██╗░░░██╗██╗░░░██╗░█████╗░██████╗░░█████╗░███████╗████████╗  ██╗░░░░░░█████╗░██╗░░░██╗███████╗████████╗
# ██╔══██╗██║░░░██║╚██╗░██╔╝██╔══██╗██╔══██╗██╔══██╗██╔════╝╚══██╔══╝  ██║░░░░░██╔══██╗██║░░░██║██╔════╝╚══██╔══╝
# ██████╦╝██║░░░██║░╚████╔╝░██║░░╚═╝██████╔╝███████║█████╗░░░░░██║░░░  ██║░░░░░███████║╚██╗░██╔╝█████╗░░░░░██║░░░
# ██╔══██╗██║░░░██║░░╚██╔╝░░██║░░██╗██╔══██╗██╔══██║██╔══╝░░░░░██║░░░  ██║░░░░░██╔══██║░╚████╔╝░██╔══╝░░░░░██║░░░
# ██████╦╝╚██████╔╝░░░██║░░░╚█████╔╝██║░░██║██║░░██║██║░░░░░░░░██║░░░  ███████╗██║░░██║░░╚██╔╝░░███████╗░░░██║░░░
# ╚═════╝░░╚═════╝░░░░╚═╝░░░░╚════╝░╚═╝░░╚═╝╚═╝░░╚═╝╚═╝░░░░░░░░╚═╝░░░  ╚══════╝╚═╝░░╚═╝░░░╚═╝░░░╚══════╝░░░╚═╝░░░

# ░█████╗░███████╗  ██╗░░░██╗░█████╗░██████╗░░█████╗░███████╗███████╗███╗░░██╗
# ██╔══██╗██╔════╝  ╚██╗░██╔╝██╔══██╗██╔══██╗██╔══██╗╚════██║██╔════╝████╗░██║
# ███████║█████╗░░  ░╚████╔╝░██║░░██║██║░░██║███████║░░███╔═╝█████╗░░██╔██╗██║
# ██╔══██║██╔══╝░░  ░░╚██╔╝░░██║░░██║██║░░██║██╔══██║██╔══╝░░██╔══╝░░██║╚████║
# ██║░░██║██║░░░░░  ░░░██║░░░╚█████╔╝██████╔╝██║░░██║███████╗███████╗██║░╚███║
# ╚═╝░░╚═╝╚═╝░░░░░  ░░░╚═╝░░░░╚════╝░╚═════╝░╚═╝░░╚═╝╚══════╝╚══════╝╚═╝░░╚══╝


options:
    mainHead: eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNjhiZGM3YTFkNmNmZjc2YTkyNTU2NTJkMzE2NTUzMjI4NWFjYzNhOWQxYzBmMTJmMzljYTAwNzc2OWE3ZWExNCJ9fX0=

function buycraft(player: player,type: string = "start"):
    set {_glass::*} to (yml list "GUI_Glass.Slots" from "Config")
    set {_glasstype} to ((yml value "GUI_Glass.Item" from "Config") parsed as item)
    if {_type} = "start":
        set {_g} to chest inventory with 3 rows named "&f&l        ❁ &3&lBuycraft &f&l❁"
        set slot {_glass::*} of {_g} to {_glasstype} named "&f"
        set slot 15 of {_g} to getHead("{@mainHead}") named "&b&lBuycraft&f&l:" with lore "%nl%&fVenstreklik:%nl%&f- &7&oTilgå vores &b&nBuycraft" and "%nl%&fHøjreklik:%nl%&f- &7&oLæs vores &b&nTerms of Service"
        set {_lore::*} to "&7Du har lige nu &b&n%{Shards::%{_player}'s uuid%} ? 0% shards&f!", "", "&fKøbshistorik:" and join (reversed ({Purchases::%{_player}'s uuid%::*} ? "&cIngen tidligere køb")) with nl
        set slot 13 of {_g} to skull of {_player} named "&b&l%{_player}%&f&l:" with lore join {_lore::*} with nl
        set slot 11 of {_g} to prismarine shard named "&b&lShard Shop&f&l:" with lore "&7For at få &nSHARDS&7 kan du%nl%&7oprette en anmodning via:%nl%&f - &b/Buy request <antal>"
    else if {_type} = "main":
        set {_g} to chest inventory with 3 rows named "&f&l        ❃ &3&lBuycraft &f&l❃"
        set slot {_glass::*} of {_g} to {_glasstype} named "&f"
        loop yml node keys "Buycraft" from "Config":
            set {_slot} to (yml value "%loop-node%.Slot" from "Config")
            set {_item} to ((yml value "%loop-node%.Item" from "Config") parsed as item)
            set {_value} to (yml value "%loop-node%.Value" from "Config")
            set {_title} to colored (yml value "%loop-node%.Title" from "Config")
            set {_lore::*} to colored (yml list "%loop-node%.Lore" from "Config")
            if {_item} = player head:
                set {_item} to getHead("%{_value}%")
            set slot {_slot} of {_g} to {_item} named placeholder_replace({_title},{_player}) with lore placeholder_replace({_lore::*},{_player})
    else if {_type} = "TOS":
        set {_g} to chest inventory with 4 rows named "&f    &l❁ &3&lTerms of Service &f&l❁"
        set slot (integers from 0 to 8) of {_g} to light blue glass pane named "&f"
        set slot (integers from 27 to 35) of {_g} to light blue glass pane named "&f"
        set slot 31 of {_g} to light blue dye named "&c&lGå tilbage!"
        loop yml node keys "Teams_OF_Service" from "Config":
            set {_slot} to (yml value "%loop-node%.Slot" from "Config")
            set {_name} to (2nd element of (split "%loop-node%" at "."))
            set {_lore::*} to (yml list "%loop-node%.Lore" from "Config")
            set slot {_slot} of {_g} to paper named "&b&lBetingelse: &f&l##%{_name}%" with lore {_lore::*}
        set slot 9 of {_g} to paper named "&b&lBetingelse: &f&l##1" with lore " &f- &7Du kan &nikke&7 få REFUND"
        set slot 10 of {_g} to paper named "&b&lBetingelse: &f&l##2" with lore " &f- &7Vi forbeholder os retten til %nl%   &7at ændre disse servicevilkår %nl%   &7efter vores eget skøn."
        set slot 11 of {_g} to paper named "&b&lBetingelse: &f&l##3" with lore " &f- &7Vi forbeholder os retten til %nl%   &7at fjerne dine goder ved %nl%   &7overtrædelse af vores reglement."
    else:
        set {_g} to chest inventory with 3 rows named "&f&l   ❃ &3&lBuycraft - %{_type}% &f&l❃"
        set slot 18 of {_g} to light blue dye named "&c&lGå tilbage!"
        set slot {_glass::*} of {_g} to {_glasstype} named "&f"
        loop yml node keys "Buycraft.%{_type}%" from "Config":
            set {_slot} to (yml value "%loop-node%.Slot" from "Config")
            set {_item} to ((yml value "%loop-node%.Item" from "Config") parsed as item)
            set {_value} to (yml value "%loop-node%.Value" from "Config")
            set {_title} to colored (yml value "%loop-node%.Title" from "Config")
            set {_lore::*} to colored (yml list "%loop-node%.Lore" from "Config")
            if {_item} = player head:
                set {_item} to getHead("%{_value}%")
            set slot {_slot} of {_g} to {_item} named placeholder_replace({_title},{_player}) with lore placeholder_replace({_lore::*},{_player})
    open {_g} to {_player}
